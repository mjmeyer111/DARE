/*CHAPTER 2 SPSS CODE*/
* NORMAL Z.
DATA LIST LIST/N A D.
BEGIN DATA.
62 ,.05 ,.5
END DATA.
COMPUTE NC=D*SQRT(N/2).
COMPUTE P1=1-CDF.NORMAL(IDF.NORMAL(1-A,0,1)-NC,0,1).
COMPUTE P2=CDF.NORMAL(NC - IDF.NORMAL(1-A/2,0,1),0,1)+CDF.NORMAL(IDF.NORMAL(A/2,0,1)-NC,0,1).
EXECUTE.

*ONE SAMPLE T TEST.
DATA LIST LIST/N D.
BEGIN DATA
30 -.5
END DATA.
COMPUTE P=NCDF.T(IDF.T(.05,N-1),N-1,D*SQRT(N)).
EXECUTE.

*INDEPENDENT T .
DATA LIST LIST/ N1 N2 A D.
BEGIN DATA
65,65,.05,.5
END DATA.
COMPUTE V=N1+N2-2.
COMPUTE NC=D*SQRT(N1*N2/(N1+N2)).
COMPUTE P=1-NCDF.T(IDF.T(1-A/2,V),V,NC)+NCDF.T(IDF.T(A/2,V),V,NC).
EXECUTE.

* STATISTICAL POWER VS N IN INDEPENDENT T.
NEW FILE.
INPUT PROGRAM.
COMPUTE A=.05.
COMPUTE STDEV=100. 
COMPUTE N=0. 
COMPUTE V=0. 
COMPUTE NC1=0. 
COMPUTE NC2=0.
COMPUTE P1=0.
COMPUTE P2=0.

LEAVE A TO P2.
 LOOP N=20 TO 300 BY 5. 
   COMPUTE V=2*N-2.
   COMPUTE NC1=30/STDEV*SQRT(N/2).  
   COMPUTE NC2=50/STDEV*SQRT(N/2).
   COMPUTE P1=1- NCDF.T(IDF.T(1-A/2,V),V,NC1) + NCDF.T(IDF.T(A/2,V),V,NC1).
   COMPUTE P2=1- NCDF.T(IDF.T(1-A/2,V),V,NC2) + NCDF.T(IDF.T(A/2,V),V,NC2).
   END CASE.
 END LOOP.
END FILE.
END INPUT PROGRAM.
EXECUTE.

DELETE VARIABLES A STDEV V NC1 NC2.
VARIABLE LABELS P1 'Power for ES .3' P2  'Power for ES .5'.
*LIST.

GRAPH
  /LINE(MULTIPLE)=VALUE(P1 P2) BY N
  /TITLE='Statistical power vs sample size in independent t test'.

*DEPENDENT T.
DATA LIST LIST/ N A R D.
BEGIN DATA.
34,.05,.5,.5
END DATA.
COMPUTE NC=SQRT(N/2)*D/SQRT(1-R).
COMPUTE P=1-NCDF.T(IDF.T(1-A/2,N-1),N-1,NC)+NCDF.T(IDF.T(A/2,N-1),N-1,NC).
EXECUTE.

* STATISTICAL POWER VS N IN DEPENDENT T.
NEW FILE.
INPUT PROGRAM.
COMPUTE A=.05.
COMPUTE STDEV=10. 
COMPUTE R=.5.
COMPUTE N=0. 
COMPUTE V=0. 
COMPUTE NC1=0. 
COMPUTE NC2=0.
COMPUTE P1=0.
COMPUTE P2=0.

LEAVE A TO P2.
 LOOP N=5 TO 100 BY 5. 
   COMPUTE V=N-1.
   COMPUTE NC1=3/STDEV*SQRT(N/2)/SQRT(1-R).  
   COMPUTE NC2=5/STDEV*SQRT(N/2)/SQRT(1-R).
   COMPUTE P1=1- NCDF.T(IDF.T(1-A/2,V),V,NC1) + NCDF.T(IDF.T(A/2,V),V,NC1).
   COMPUTE P2=1- NCDF.T(IDF.T(1-A/2,V),V,NC2) + NCDF.T(IDF.T(A/2,V),V,NC2).
   END CASE.
 END LOOP.
END FILE.
END INPUT PROGRAM.
EXECUTE.

DELETE VARIABLES A STDEV V NC1 NC2.
VARIABLE LABELS P1 'Power for ES .3' P2  'Power for ES .5'.
*LIST.

GRAPH
  /LINE(MULTIPLE)=VALUE(P1 P2) BY N
  /TITLE='Statistical power vs sample size in dependent t test'.

