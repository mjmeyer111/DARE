* HOTELLING T.
SET MXLOOPS=1E3.
MATRIX.
COMPUTE D={40/120; 70/150}.
COMPUTE R={1,.35;.35, 1}.

LOOP N=20 TO 120.
 COMPUTE V1=NROW(D).
 COMPUTE V2=2*N-V1-1.
 COMPUTE LAMBDA=.5*N*T(D)*INV(R)*D.
 COMPUTE CASE={N,V1,V2,LAMBDA}.
 SAVE CASE /OUTFILE=* /VARIABLES= N V1 V2 LAMBDA.
END LOOP.

END MATRIX.

COMPUTE ALPHA=.05.
COMPUTE POWER=1-NCDF.F(IDF.F(1-ALPHA,V1,V2),V1,V2,LAMBDA).
EXECUTE.

DELETE VARIABLES V1 V2  ALPHA LAMBDA.
EXECUTE.

GRAPH
  /LINE(SIMPLE)=VALUE(POWER) BY N
  /TITLE='Statisticl power vs n in Hotelling T^2'.

*****************************************************.
*MANOVA.
SET MXLOOPS=1E3.

MATRIX.
COMPUTE SD={120, 150,1}.
COMPUTE D=MDIAG(SD).
COMPUTE R={1, .35, .62; .35, 1, .22; .62, .22, 1}.
COMPUTE D1={50,20,0}.
COMPUTE D2={70,20,0}.
COMPUTE D3={.5,.2,0}.
COMPUTE J=MAKE(1,3,1).

COMPUTE A1=D1-MSUM(D1)/NCOL(D1)*J.
COMPUTE A2=D2-MSUM(D2)/NCOL(D2)*J.
COMPUTE A3=D3-MSUM(D3)/NCOL(D3)*J.
COMPUTE A={A1;A2;A3}.

COMPUTE E=D*R*D.
COMPUTE P=NROW(A).
COMPUTE K=NCOL(A).

LOOP N=35 TO 75.
  COMPUTE V1=K-1.
  COMPUTE V2=(N-1)*K.
  COMPUTE T1=SQRT((P**2*V1**2-4)/(P**2+V1**2-5)).
  COMPUTE W=V1+V2-.5*(P+V1+1).
  COMPUTE DF1=V1*P.
  COMPUTE DF2=W*T1-.5*(P*V1-2).
 
  COMPUTE A2=A*T(A).
  COMPUTE BIGA=N*A2.

  COMPUTE L=DET(V2*E)/DET(V2*E+BIGA).
  COMPUTE NC=(L**(-1/T1)-1)*DF2.
  COMPUTE CASE={N, DF1, DF2, NC}.
  SAVE  CASE /OUTFILE=* /VARIABLES=N DF1 DF2  NC.
END LOOP.

END MATRIX.

COMPUTE ALPHA=.05.
COMPUTE POWER=1-NCDF.F(IDF.F(1-ALPHA,DF1,DF2),DF1,DF2,NC).
EXECUTE.

DELETE VARIABLES DF1 DF2  ALPHA NC.
EXECUTE.
  
***************************************************.

* MANCOVA  .

SET MXLOOPS=1E3.

MATRIX.
COMPUTE SD={120, 150,1}.
COMPUTE D=MDIAG(SD).
COMPUTE R={1, .35, .62; .35, 1, .22; .62, .22, 1}.
COMPUTE D1={50,20,0}.
COMPUTE D2={70,20,0}.
COMPUTE D3={.5,.2,0}.
COMPUTE J=MAKE(1,3,1).

COMPUTE A1=D1-MSUM(D1)/NCOL(D1)*J.
COMPUTE A2=D2-MSUM(D2)/NCOL(D2)*J.
COMPUTE A3=D3-MSUM(D3)/NCOL(D3)*J.
COMPUTE A={A1;A2;A3}.

COMPUTE RYX={.64; .34; .53}.
COMPUTE EX=D*(R-RYX*T(RYX))*D.
COMPUTE P=NROW(A).
COMPUTE K=NCOL(A).
COMPUTE Q=NCOL(RYX).

LOOP N=30 TO 60.
  COMPUTE V1=K-1.
  COMPUTE V2=(N-1)*K-Q.
  COMPUTE T1=SQRT((P**2*V1**2-4)/(P**2+V1**2-5)).
  COMPUTE W=V1+V2-.5*(P+V1+1).
  COMPUTE DF1=V1*P.
  COMPUTE DF2=W*T1-.5*(P*V1-2).
 
  COMPUTE A2=A*T(A).
  COMPUTE BIGA=N*A2.

  COMPUTE L=DET(V2*EX)/DET(V2*EX+BIGA).
  COMPUTE NC=(L**(-1/T1)-1)*DF2.
  COMPUTE CASE={N, DF1, DF2, NC}.
  SAVE  CASE /OUTFILE=* /VARIABLES=N DF1 DF2  NC.
END LOOP.

END MATRIX.

COMPUTE ALPHA=.05.
COMPUTE POWER=1-NCDF.F(IDF.F(1-ALPHA,DF1,DF2),DF1,DF2,NC).
EXECUTE.

DELETE VARIABLES DF1 DF2  ALPHA NC.
EXECUTE.



