*multivariate analysis of linear change btw two groups .
MATRIX.
COMPUTE C1={-1,1,0,0;
                        0,-1,1,0;
                        0,0,-1,1}.
COMPUTE E={
135.3863636,67.92045455,97.75568182,67.75568182 ;
67.92045455,104.6193182,73.17897727,82.92897727;
97.75568182,73.17897727,161.3934659,103.2684659;
67.75568182,82.92897727,103.2684659,124.6434659}.
COMPUTE S=E/(16+11-2).
COMPUTE CSC=C1*S*T(C1).
COMPUTE N1=22.
COMPUTE N2=22.
COMPUTE BETA_D=.7.
COMPUTE P=NROW(CSC)+1.
COMPUTE J=MAKE(P-1,1,1).
COMPUTE LAMBDA=N1*N2/(N1+N2)*BETA_D**2*(T(J)*INV(CSC)*J).
SAVE {N1,N2,P, BETA_D, LAMBDA} /OUTFILE=*/VARIABLES=N1
N2 P BETA_D LAMBDA .
END MATRIX.
COMPUTE A=.05.
COMPUTE F0=IDF.F(1-A,P-1,N1+N2-P).
COMPUTE POWER=1-NCDF.F(F0,P-1,N1+N2-P,LAMBDA).
EXECUTE.

* multivariate analysis of quadratic change in one group.
MATRIX.
COMPUTE C1={-1,1,0,0;
0,-1,1,0;
0,0,-1,1}.
COMPUTE C2={-1,1,0;
0,-1,1}.
COMPUTE C=C2*C1.
COMPUTE E={
1289.664,1205.548,1027.072,1044.246;
1205.548,1247.069333,1021.377333,1004.818667;
1027.072,1021.377333,1006.469333,890.2846667;
1044.246,1004.818667,890.2846667,1056.217333}.
COMPUTE S=E/14.
COMPUTE CSC=C*S*T(C).
COMPUTE N=9.
COMPUTE BETA2=-1.96.
COMPUTE P=NROW(CSC)+2.
COMPUTE J=MAKE(P-2,1,1).
COMPUTE LAMBDA=4*N*BETA2**2*(T(J)*INV(CSC)*J).

SAVE {N,P,BETA2,LAMBDA}
/OUTFILE=*/VARIABLES=N P BETA2 LAMBDA.
END MATRIX.

COMPUTE A=.05.
COMPUTE F0=IDF.F(1-A,P-2,N-P+2).
COMPUTE POWER=1-NCDF.F(F0,P-2,N-P+2,LAMBDA).
EXECUTE.

* HLM random coefficients:
compare linear change between two groups .
MATRIX.
COMPUTE M=3.
COMPUTE T0=MAKE(M+1,1,1).
COMPUTE T1=T({0:M}).
COMPUTE Z1=T1-T(T1)*T0/CSSQ(T0)*T0.
COMPUTE N=112.
COMPUTE BETA11=.187.
COMPUTE SIGMA2=.42.
COMPUTE P1=.04/.42.

COMPUTE TAU1=SIGMA2*P1.
COMPUTE LAMBDA=SQRT(N/4)*BETA11/SQRT(TAU1+SIGMA2/CSSQ(Z1)).
SAVE {M,N,BETA11,SIGMA2,P1, LAMBDA }
/OUTFILE=* /VARIABLES= M N BETA11 SIGMA2 P1 LAMBDA.
END MATRIX.
COMPUTE A=.05.
COMPUTE T0=IDF.T(1-A/2,N-2).
COMPUTE POWER=1-NCDF.T(T0,N-2,LAMBDA)+NCDF.T(-T0,N-2,LAMBDA).
PRINT /POWER.
EXECUTE.

* HLM random coefficients: quadratic change in one group.
MATRIX.
COMPUTE M=6.
COMPUTE T0=MAKE(M+1,1,1).
COMPUTE T1=T({0:M}).
COMPUTE T2=T1&**2/2.
COMPUTE Z1=T1-T(T1)*T0/CSSQ(T0)*T0.
COMPUTE Z2=T2-T(T2)*T0/CSSQ(T0)*T0-T(T2)*Z1/CSSQ(Z1)*Z1.

COMPUTE N=14.
COMPUTE BETA20=-.707.
COMPUTE SIGMA=3.75.
COMPUTE P2=(.24/3.75)**2.
COMPUTE TAU2=SIGMA**2*P2.
COMPUTE LAMBDA=SQRT(N)*BETA20/SQRT(TAU2+SIGMA**2/CSSQ(Z2)).
SAVE {M,N,BETA20,SIGMA,P2, LAMBDA }
/OUTFILE=* /VARIABLES= M N BETA20 SIGMA P2 LAMBDA .
END MATRIX.
COMPUTE A=.05.
COMPUTE T0=IDF.T(1-A/2,N-1).
COMPUTE POWER=1-NCDF.T(T0,N-1,LAMBDA)+NCDF.T(-T0,N-1,LAMBDA).
PRINT /POWER.
EXECUTE.
