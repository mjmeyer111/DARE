*STATISTICAL POWER IN ONE-WAY ANOVA.
NEW FILE.
INPUT PROGRAM.
COMPUTE A=.05.
COMPUTE STDEV=4. 
COMPUTE N=0. 
COMPUTE J=3.
COMPUTE V=0. 
COMPUTE NC=0. 
COMPUTE P=0.

LEAVE A TO P.
 LOOP N=5 TO 12. 
   COMPUTE V=J*(N-1).
   COMPUTE NC=N*(J-1)*VARIANCE(8,11,15)/STDEV**2.  
   COMPUTE P=1- NCDF.F(IDF.F(1-A,J-1,V),J-1,V,NC).
   END CASE.
 END LOOP.
END FILE.
END INPUT PROGRAM.
EXECUTE.

DELETE VARIABLES A STDEV J V NC.
VARIABLE LABELS P 'Power'.
LIST.

GRAPH 
  /LINE(SIMPLE)=VALUE(P) BY N 
  /TITLE='Statistical power vs sample size in one-way ANOVA'.

*STATISTICAL POWER IN CONTRAST TESTS.
NEW FILE.
INPUT PROGRAM.
COMPUTE A=.05.
COMPUTE STDEV=4. 
COMPUTE N=0.
COMPUTE J=3. 
COMPUTE V=0. 
COMPUTE NC1=0. 
COMPUTE NC2=0.
COMPUTE P1=0.
COMPUTE P2=0.

LEAVE A TO P2.
 LOOP N=5 TO 30. 
   COMPUTE V=J*(N-1).
   COMPUTE NC1=(15*.5+11*.5-8)/STDEV*SQRT(N)/SQRT((J-1)*VARIANCE(.5,.5,-1)).  
   COMPUTE NC2=(15*1+11*-1)/STDEV*SQRT(N)/SQRT((J-1)*VARIANCE(1,-1,0)).  
   COMPUTE P1=1- NCDF.T(IDF.T(1-A/2,V),V,NC1) + NCDF.T(IDF.T(A/2,V),V,NC1).
   COMPUTE P2=1- NCDF.T(IDF.T(1-A/2,V),V,NC2) + NCDF.T(IDF.T(A/2,V),V,NC2).
   END CASE.
 END LOOP.
END FILE.
END INPUT PROGRAM.
EXECUTE.

DELETE VARIABLES A STDEV V NC1 NC2.
VARIABLE LABELS P1 'Power for contrast (.5,.5,-1)' 
                               P2 'Power for contrast (1,-1,0)' .
LIST.

GRAPH
  /LINE(MULTIPLE)=VALUE(P1 P2) BY N
  /TITLE='Statistical power for contrast (.5,.5,-1) and (1,-1,0)'.


*STATISTICAL POWER IN RANDOM EFFECTS ANOVA.
NEW FILE.
INPUT PROGRAM.
COMPUTE A=.05.
COMPUTE RHO=.30. 
COMPUTE N=20. 
COMPUTE M=(1-RHO)/(N*RHO+(1-RHO)).
COMPUTE V=0.
COMPUTE J=0.
COMPUTE P=0.

LEAVE A TO P.
 LOOP J=3 TO 6. 
   COMPUTE V=J*(N-1).
   COMPUTE P=1- CDF.F(IDF.F(1-A,J-1,V)*M,J-1,V).
   END CASE.
 END LOOP.
END FILE.
END INPUT PROGRAM.
EXECUTE.

DELETE VARIABLES A RHO N M V.
VARIABLE LABELS P 'Power'.
LIST.
